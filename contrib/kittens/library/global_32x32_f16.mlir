// Kittens 32x32_f16 tile abstractions for global load/store.
// Uses amdgcn.ptr_add with dynamic VGPR offsets and lsir.alloca for destinations.

// Register types
!sx2 = !amdgcn.sgpr<[? + 2]>
!s   = !amdgcn.sgpr
!v   = !amdgcn.vgpr
!vx2 = !amdgcn.vgpr<[? + 2]>
!vx16 = !amdgcn.vgpr<[? + 16]>

// Token types for async memory operations
!write_token = !amdgcn.write_token<flat>
!wtok_buf = memref<?x!write_token>

// Future types for global loads
!future_global_read = !aster_utils.struct<value: !aster_utils.any, token: !amdgcn.read_token<flat>>
!gfut_buf = memref<?x!future_global_read>

// Descriptor types
!index_pair = !aster_utils.struct<i: index, j: index>
!index_descriptor_2d = !aster_utils.struct<i: index, j: index, stride: index, elt_size_b: index>

// Kittens register tile type aliases for 32x32x8 MFMA
!rt_C_f32 = !vx16

amdgcn.library @kittens_global_32x32_f16 isa = [#amdgcn.isa<cdna3>] {
  // From register-init.mlir
  func.func private @init_vgprx16(i32) -> !vx16
  // From indexing.mlir (non-ptr versions still needed for MFMA indexing)
  func.func private @mfma_index_C_32x32xf32() -> !index_pair
  func.func private @mfma_c_row_32x32xf32(index, index) -> index
  func.func private @thread_tile_pos_32x32() -> (index, index)
  // From indexing_ptr.mlir
  func.func private @matrix_offset_idx(!index_descriptor_2d) -> index
  func.func private @tile_thread_offset_idx(!index_descriptor_2d, index) -> index
  func.func private @index_to_vgpr_i32(index) -> !v

  //===--------------------------------------------------------------------===//
  // Tile initialization
  //===--------------------------------------------------------------------===//

  func.func private @zero_C_32x32() -> !rt_C_f32 {
    %c0 = arith.constant 0 : i32
    %result = func.call @init_vgprx16(%c0) : (i32) -> !vx16
    return %result : !rt_C_f32
  }

  //===--------------------------------------------------------------------===//
  // Global Load (32x32 tile, ptr-based addressing)
  //===--------------------------------------------------------------------===//

  // Issue 4 global loads for a 32x32 f16 tile using ptr_add addressing.
  // Each load computes a total byte offset = tile_offset + thread_offset,
  // converts to a VGPR, and uses amdgcn.ptr_add to form the address.
  func.func private @load_global_tile_32x32_f16(
      %ptr: !sx2, %m: index, %k_base: index, %stride: index
  ) -> !gfut_buf {
    %row_in_group, %col = func.call @thread_tile_pos_32x32() : () -> (index, index)
    %elt_size = arith.constant 2 : index
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %c4 = arith.constant 4 : index

    // Thread-level offset components (per-lane from lane_id)
    %d_desc = aster_utils.struct_create(%row_in_group, %col, %stride, %elt_size)
        : (index, index, index, index) -> !index_descriptor_2d
    %d_off = func.call @matrix_offset_idx(%d_desc) : (!index_descriptor_2d) -> index

    %buf = memref.alloca(%c4) : !gfut_buf
    scf.for %g = %c0 to %c4 step %c1 {
      // source address calculation
      %tile_row = affine.apply affine_map<(g)[m] -> (m + g * 8)>(%g)[%m]
      %u_desc = aster_utils.struct_create(%tile_row, %k_base, %stride, %elt_size)
          : (index, index, index, index) -> !index_descriptor_2d
      %total_off = func.call @tile_thread_offset_idx(%u_desc, %d_off)
          : (!index_descriptor_2d, index) -> index
      %total_reg = func.call @index_to_vgpr_i32(%total_off) : (index) -> !v
      %addr = amdgcn.ptr_add %ptr d_off = %total_reg : !sx2, !v

      // load
      %tmp = lsir.alloca : !vx2
      %loaded, %tok = amdgcn.load global_load_dwordx2 dest %tmp addr %addr
          : dps(!vx2) ins(!amdgcn.vgpr<[? + 2]>) -> !amdgcn.read_token<flat>
      %val = aster_utils.to_any %loaded : !vx2
      %f = aster_utils.struct_create(%val, %tok)
          : (!aster_utils.any, !amdgcn.read_token<flat>) -> !future_global_read
      memref.store %f, %buf[%g] : !gfut_buf
    } {aster.constexpr}

    return %buf : !gfut_buf
  }

  //===--------------------------------------------------------------------===//
  // Global Store (C tile 32x32 f32, ptr-based addressing)
  //===--------------------------------------------------------------------===//

  // Store a 32x32 f32 C tile using ptr_add addressing.
  // Each store computes total byte offset = tile_off + thread_off + reg_off.
  func.func private @store_C_32x32_f32(%tile: !rt_C_f32, %ptr: !sx2, %m: index, %n: index, %stride: index) -> !wtok_buf {
    %mfma_idx = func.call @mfma_index_C_32x32xf32() : () -> !index_pair
    %col, %row_base = aster_utils.struct_extract %mfma_idx ["i", "j"] : !index_pair -> index, index

    %c0  = arith.constant 0 : index
    %c1  = arith.constant 1 : index
    %c2  = arith.constant 2 : index
    %c3  = arith.constant 3 : index
    %c4  = arith.constant 4 : index
    %c5  = arith.constant 5 : index
    %c6  = arith.constant 6 : index
    %c7  = arith.constant 7 : index
    %c8  = arith.constant 8 : index
    %c9  = arith.constant 9 : index
    %c10 = arith.constant 10 : index
    %c11 = arith.constant 11 : index
    %c12 = arith.constant 12 : index
    %c13 = arith.constant 13 : index
    %c14 = arith.constant 14 : index
    %c15 = arith.constant 15 : index
    %c16 = arith.constant 16 : index

    // Split !vx16 into 16 individual VGPRs and pack into buffer for iteration.
    %r:16 = amdgcn.split_register_range %tile : !vx16
    %reg_buf = memref.alloca(%c16) : memref<?x!aster_utils.any>
    %a0  = aster_utils.to_any %r#0  : !v
    %a1  = aster_utils.to_any %r#1  : !v
    %a2  = aster_utils.to_any %r#2  : !v
    %a3  = aster_utils.to_any %r#3  : !v
    %a4  = aster_utils.to_any %r#4  : !v
    %a5  = aster_utils.to_any %r#5  : !v
    %a6  = aster_utils.to_any %r#6  : !v
    %a7  = aster_utils.to_any %r#7  : !v
    %a8  = aster_utils.to_any %r#8  : !v
    %a9  = aster_utils.to_any %r#9  : !v
    %a10 = aster_utils.to_any %r#10 : !v
    %a11 = aster_utils.to_any %r#11 : !v
    %a12 = aster_utils.to_any %r#12 : !v
    %a13 = aster_utils.to_any %r#13 : !v
    %a14 = aster_utils.to_any %r#14 : !v
    %a15 = aster_utils.to_any %r#15 : !v
    memref.store %a0,  %reg_buf[%c0]  : memref<?x!aster_utils.any>
    memref.store %a1,  %reg_buf[%c1]  : memref<?x!aster_utils.any>
    memref.store %a2,  %reg_buf[%c2]  : memref<?x!aster_utils.any>
    memref.store %a3,  %reg_buf[%c3]  : memref<?x!aster_utils.any>
    memref.store %a4,  %reg_buf[%c4]  : memref<?x!aster_utils.any>
    memref.store %a5,  %reg_buf[%c5]  : memref<?x!aster_utils.any>
    memref.store %a6,  %reg_buf[%c6]  : memref<?x!aster_utils.any>
    memref.store %a7,  %reg_buf[%c7]  : memref<?x!aster_utils.any>
    memref.store %a8,  %reg_buf[%c8]  : memref<?x!aster_utils.any>
    memref.store %a9,  %reg_buf[%c9]  : memref<?x!aster_utils.any>
    memref.store %a10, %reg_buf[%c10] : memref<?x!aster_utils.any>
    memref.store %a11, %reg_buf[%c11] : memref<?x!aster_utils.any>
    memref.store %a12, %reg_buf[%c12] : memref<?x!aster_utils.any>
    memref.store %a13, %reg_buf[%c13] : memref<?x!aster_utils.any>
    memref.store %a14, %reg_buf[%c14] : memref<?x!aster_utils.any>
    memref.store %a15, %reg_buf[%c15] : memref<?x!aster_utils.any>

    // Thread-level offset: row_base * stride + col * elt_size (per-lane)
    %elt_size = arith.constant 4 : index
    %d_desc = aster_utils.struct_create(%row_base, %col, %stride, %elt_size)
        : (index, index, index, index) -> !index_descriptor_2d
    %d_off = func.call @matrix_offset_idx(%d_desc) : (!index_descriptor_2d) -> index

    %tok_buf = memref.alloca(%c16) : memref<?x!write_token>
    scf.for %i = %c0 to %c16 step %c1 {
      %any_reg = memref.load %reg_buf[%i] : memref<?x!aster_utils.any>
      %reg = aster_utils.from_any %any_reg : !v

      // target address calculation
      %reg_row_const = func.call @mfma_c_row_32x32xf32(%c0, %i) : (index, index) -> index
      %tile_row = affine.apply affine_map<()[m, rrc] -> (m + rrc)>()[%m, %reg_row_const]
      %u_desc = aster_utils.struct_create(%tile_row, %n, %stride, %elt_size)
          : (index, index, index, index) -> !index_descriptor_2d
      %total_off = func.call @tile_thread_offset_idx(%u_desc, %d_off)
          : (!index_descriptor_2d, index) -> index
      %total_reg = func.call @index_to_vgpr_i32(%total_off) : (index) -> !v
      %addr = amdgcn.ptr_add %ptr d_off = %total_reg : !sx2, !v

      // store
      %tok = amdgcn.store global_store_dword data %reg addr %addr
          : ins(!v, !amdgcn.vgpr<[? + 2]>) -> !amdgcn.write_token<flat>
      memref.store %tok, %tok_buf[%i] : memref<?x!write_token>
    } {aster.constexpr}

    return %tok_buf : !wtok_buf
  }

  // Wait for all global store tokens from store_C_32x32_f32.
  func.func private @wait_global_writes_32x32(%tok_buf: !wtok_buf) {
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %c16 = arith.constant 16 : index
    scf.for %i = %c0 to %c16 step %c1 {
      %tok = memref.load %tok_buf[%i] : !wtok_buf
      amdgcn.wait deps %tok : !write_token
    } {aster.constexpr}
    return
  }
}
