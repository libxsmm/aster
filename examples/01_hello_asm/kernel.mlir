// Hello ASTER: compute 32 + 10 = 42, self-check, trap if wrong.
// Compare MLIR directly to assembly output -- this is WYSIWYG.
//
// Key concepts:
//   amdgcn.module  - target GPU module (gfx942 = MI300X)
//   kernel / end_kernel - kernel entry/exit
//   alloca : !amdgcn.vgpr<N> - allocate physical register vN
//   vop2 v_add_u32 outs %dst ins %a, %b - DPS: destination-passing style
//   cmpi v_cmp_ne_i32 - vector compare (sets VCC register)
//   cbranch s_cbranch_vccnz - conditional branch on VCC
//   s_trap 2 - hardware trap (GPU abort)

module {
  amdgcn.module @hello target = <gfx942> isa = <cdna3> {
    kernel @kernel {
    ^entry:
      %v0 = alloca : !amdgcn.vgpr<0>
      %v1 = alloca : !amdgcn.vgpr<1>
      %v2 = alloca : !amdgcn.vgpr<2>

      %c32 = arith.constant 32 : i32
      amdgcn.vop1.vop1 <v_mov_b32_e32> %v1, %c32 : (!amdgcn.vgpr<1>, i32) -> ()

      %c10 = arith.constant 10 : i32
      amdgcn.vop1.vop1 <v_mov_b32_e32> %v2, %c10 : (!amdgcn.vgpr<2>, i32) -> ()

      // v0 = v1 + v2 = 32 + 10 = 42
      vop2 v_add_u32 outs %v0 ins %v1, %v2 : !amdgcn.vgpr<0>, !amdgcn.vgpr<1>, !amdgcn.vgpr<2>

      // Self-check: trap if result != 42
      %vcc = amdgcn.alloca : !amdgcn.vcc
      %c42 = arith.constant 42 : i32
      amdgcn.cmpi v_cmp_ne_i32 outs %vcc ins %c42, %v0
        : outs(!amdgcn.vcc) ins(i32, !amdgcn.vgpr<0>)
      amdgcn.cbranch s_cbranch_vccnz %vcc ^trap fallthrough(^ok)
        : !amdgcn.vcc

    ^ok:
      end_kernel

    ^trap:
      amdgcn.sopp.sopp #amdgcn.inst<s_trap>, imm = 2
      end_kernel
    }
  }
}
